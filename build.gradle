plugins {
	id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}

	exclusiveContent {
		forRepository {
			maven {
				url "https://cursemaven.com"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	// 从SSC上复制的
	maven {
		url 'https://maven.aliyun.com/nexus/content/groups/public'
	}
	maven { url 'https://maven.azuredoom.com/mods' }
	maven { url 'https://dl.cloudsmith.io/public/azuredoom-mods/azurelib/maven/' }
	maven { url 'https://maven.wispforest.io/releases/' }
	maven {
		name = "QuiltMC"
		url = "https://maven.quiltmc.org/repository/release"
	}
	maven {
		name = "Ladysnake Mods"
		url = "https://maven.ladysnake.org/releases"
	}
	maven {
		url = "https://maven.cafeteria.dev"
		content {
			includeGroup "net.adriantodt.fabricmc"
		}
	}
	maven {
		url = "https://maven.jamieswhiteshirt.com/libs-release"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
	maven {
		url = "https://masa.dy.fi/maven"
	}
	maven {
		url = "https://jitpack.io"
	}
	maven {
		url = "https://maven.shedaniel.me"
	}
	maven {
		url = "https://maven.terraformersmc.com"
	}
	maven {
		name "KosmX's maven"
		url 'https://maven.kosmx.dev/'
	}
	maven {
		name = "Fuzs Mod Resources"
		url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
	}
	maven { url "https://maven.ryanliptak.com/" }
	mavenLocal()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	modImplementation "maven.modrinth:shape-shifter-curse:${ssc_version}"
	modRuntimeOnly "curse.maven:rich-translatable-text-1342784:7440321"  // 1.0.2

	// SSC 的依赖
	modImplementation "mod.azure.azurelib:azurelib-fabric-1.20.1:${azurelib_version}"
	modImplementation "org.ladysnake:satin:${satin_version}"
	modImplementation "io.github.apace100:apoli:${apoli_version}"
	modApi("com.github.Virtuoel:Pehkui:${pehkui_version}", {
		exclude group: "net.fabricmc.fabric-api"
	})
	modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi("me.shedaniel.cloth:cloth-config-fabric:${clothconfig_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modImplementation "com.terraformersmc:modmenu:${mod_menu_version}"
	modImplementation "dev.emi:trinkets:${trinkets_version}"

}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	repositories {
	}
}